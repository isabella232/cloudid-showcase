buildscript {
    ext {
        springBootVersion = '2.0.0.RELEASE'
    }
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.4.0'
    }
}
plugins {
    id 'org.sonarqube' version '2.6.2'
}


apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.google.osdetector'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    grpcVersion = '1.5.0'
    nettyVersion = '4.1.12.Final'
    nettyTcnativeVersion = '2.0.5.Final'
    bouncyCastleVersion = '1.59'
    lombokVersion = '1.16.20'
}

dependencies {
    // Spring Boot
    compile "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    // Spring Restdocs
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.0.RELEASE'

    compile 'org.apache.httpcomponents:httpclient:4.5.4'

    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "com.google.api.grpc:googleapis-common-protos:0.0.3"
    compile "io.netty:netty-transport-native-epoll:${nettyVersion}:linux-x86_64"
    compile "io.netty:netty-transport-native-kqueue:${nettyVersion}:osx-x86_64"
    compile "io.netty:netty-tcnative-boringssl-static:${nettyTcnativeVersion}"

    // Apache Commons Lang 3
    compile 'org.apache.commons:commons-lang3:3.7'

    // BouncyCastle
    compile "org.bouncycastle:bcprov-jdk15on:${bouncyCastleVersion}"
    compile "org.bouncycastle:bcpkix-jdk15on:${bouncyCastleVersion}"

    // Lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"

    // Spock tests
    testCompile "org.codehaus.groovy:groovy-all:2.4.13"
    testCompile "org.spockframework:spock-core:1.1-groovy-2.4"
    testCompile "org.hamcrest:hamcrest-core:1.3"
    testRuntime "net.bytebuddy:byte-buddy:1.6.5"
    testRuntime "org.objenesis:objenesis:2.5.1"

    // WireMock
    // Use standalone as WireMock depends on an outdated version of Jetty that clashes with Spring Boot
    testCompile "com.github.tomakehurst:wiremock-standalone:2.14.0"
}


bootJar {
    archiveName "${project.name}.jar"
}


protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.3.0"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {

            }
        }
    }
}

jacoco {
    toolVersion = "0.8.0"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

sourceSets {

    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
    test {
        java {
            srcDirs = []
        }
        groovy {
            srcDirs 'src/test/java'
        }
    }
}

sonarqube {
    properties {
        property 'sonar.exclusions', '**/*Generated.java, build/generated/**'
        property 'sonar.projectName', 'Cloud ID SPIRE Demo-Server'

        property 'sonar.issue.ignore.multicriteria', 'e1,e2'

        // Rule attempts to enforce consistent logger field names, but is broken as it also triggers on variables.
        property 'sonar.issue.ignore.multicriteria.e1.ruleKey', 'squid:S1312'
        property 'sonar.issue.ignore.multicriteria.e1.resourceKey', '**'

        // Rule disallows wildcard import which clashes with IntelliJ presets
        property 'sonar.issue.ignore.multicriteria.e2.ruleKey', 'squid:S2208'
        property 'sonar.issue.ignore.multicriteria.e2.resourceKey', '**'    }
}
