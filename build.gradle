buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
    }
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.17.2"
    }
}
//plugins {
//    id 'com.palantir.docker' version '0.17.2'
//}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: "com.palantir.docker"

group = 'de.qaware.cloud.id'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    springCloudVersion = 'Edgware.RELEASE'

    // create version with git-hash
    hash = 'git rev-parse --short HEAD'.execute().text.trim()
    fullVersion = "${project.version}.${hash}"
}

dependencies {
    //compile('org.springframework.cloud:spring-cloud-starter-config')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile 'org.apache.httpcomponents:httpclient:4.5.4'
    compileOnly('org.projectlombok:lombok')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

jar {
    archiveName "${project.name}.jar"
}

docker {
    name "qaware.de/cloud-id/demo-server:${fullVersion}"
    tags 'latest'
    dockerfile project.file('src/main/docker/Dockerfile')
    files jar.archivePath
}
docker.dependsOn(assemble)