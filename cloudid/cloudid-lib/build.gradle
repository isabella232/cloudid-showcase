buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.4.0'
    }
}

apply plugin: 'com.google.protobuf'
apply plugin: 'com.google.osdetector'


ext {
    grpcVersion = '1.5.0'
    nettyVersion = '4.1.12.Final'
    nettyTcnativeVersion = '2.0.5.Final'
    bouncyCastleVersion = '1.59'
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.3.0"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {

            }
        }
    }
}

dependencies {
    // GRPC
    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "com.google.api.grpc:googleapis-common-protos:0.0.3"

    // Netty for using UDS
    compile "io.netty:netty-transport-native-epoll:${nettyVersion}:linux-x86_64"
    compile "io.netty:netty-transport-native-kqueue:${nettyVersion}:osx-x86_64"
    compile "io.netty:netty-tcnative-boringssl-static:${nettyTcnativeVersion}"

    // BouncyCastle
    compile "org.bouncycastle:bcprov-jdk15on:${bouncyCastleVersion}"
    compile "org.bouncycastle:bcpkix-jdk15on:${bouncyCastleVersion}"

    // SLF4J
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    // Logback as SLF4j implementation for tests
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    // Apache Commons Lang 3
    compile 'org.apache.commons:commons-lang3:3.7'
}

compileJava.dependsOn(processResources)
